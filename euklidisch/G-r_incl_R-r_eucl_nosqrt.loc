Base_functions:={(+,2), (-,2), (*,2)}
Extension_functions:={(V, 1, 1), (E, 2, 1), (X, 1, 1), (Y, 1, 1)}
Relations:={(<,2), (>,2), (<=,2), (>=,2)}

% Problem: Liegt G-> in R->? Metrik: euklidisch ohne Wurzel, Instanzierung Pi_R
% E(u,v) -> Pi_G(u,v) und NOT(E(u,v) -> Pi_R(u,v)) ist unerfüllbar
% liefert sat

Clauses :=

% Knoten sowie Kanten sind entweder 0 oder 1
(FORALL x).	--> V(x)=_1, V(x)=_0;
(FORALL x,y).	--> E(x,y)=_1, E(x,y)=_0;

% Kante existiert zwischen zwei Knoten und ist symmetrisch
(FORALL x,y).	E(x,y)=_1 --> E(y,x)=_1;
(FORALL x,y).	E(x,y)=_1 --> V(x)=_1;

% Punkte sind gleich wenn ihre Koordinaten gleich sind
(FORALL x,y). 	x=y --> X(x)=X(y);
(FORALL x,y). 	x=y --> Y(x)=Y(y);
(FORALL x,y).	X(x)=X(y), Y(x)=Y(y) --> x=y;

Query :=

% m ist Mittelpunkt des Segments uv
_2*X(m) = X(u)+X(v);
_2*Y(m) = Y(u)+Y(v);

% Punkte sind verschieden
NOT(u=v);
NOT(u=w);
NOT(u=m);
NOT(v=w);
NOT(w=m);
NOT(v=m);

% Instanzierung E(u,v) -> Pi_G(u,v)
OR(E(u,v)=_0, V(w)=_0, ((X(m)-X(w))*(X(m)-X(w))) + ((Y(m)-Y(w))*(Y(m)-Y(w))) > ((X(u)-X(m))*(X(u)-X(m))) + ((Y(u)-Y(m))*(Y(u)-Y(m))));

% NOT(Pi_R(u,v) <- E(u,v))
% heißt E(u,v)=1 und NOT(Pi_R(u,v))
E(u,v)=_1;
V(w)=_1;
((X(u)-X(w))*(X(u)-X(w))) + ((Y(u)-Y(w))*(Y(u)-Y(w))) < ((X(u)-X(v))*(X(u)-X(v))) + ((Y(u)-Y(v))*(Y(u)-Y(v)));
((X(v)-X(w))*(X(v)-X(w))) + ((Y(v)-Y(w))*(Y(v)-Y(w))) < ((X(u)-X(v))*(X(u)-X(v))) + ((Y(u)-Y(v))*(Y(u)-Y(v)));
